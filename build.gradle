plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'mil.army.usace.hec'
version '1.1-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://www.hec.usace.army.mil/nexus/repository/maven-public/" }
}

dependencies {
    implementation 'org.json:json:20190722'
    implementation 'com.j2html:j2html:1.4.0'
    implementation 'tech.tablesaw:tablesaw-core:0.38.1'
    implementation 'tech.tablesaw:tablesaw-jsplot:0.38.1'
    implementation 'org.jsoup:jsoup:1.12.2'

    implementation ("mil.army.usace.hec:hec-monolith:2.+") { transitive(false) }
    implementation ("mil.army.usace.hec:hec-nucleus-data:1.+") { transitive(false) }
    implementation ("mil.army.usace.hec:hec-nucleus-metadata:1.+") { transitive(false) }

    // Start runtime-only deps required by HEC shared libraries
    runtimeOnly("com.google.flogger:flogger:0.7.4")
    runtimeOnly("com.google.flogger:flogger-system-backend:0.7.4")
    // End runtime-only deps required by HEC shared libraries

    testImplementation 'org.junit.jupiter:junit-jupiter:5.4.0'
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.4.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.4.0"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.4.0"
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'hms-reports'
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username = "$mavenUser"
                password = "$mavenPassword"
            }
            def releasesRepoUrl = "https://www.hec.usace.army.mil/nexus/repository/maven-releases/"
            def snapshotsRepoUrl = "https://www.hec.usace.army.mil/nexus/repository/maven-snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

publish.dependsOn jar